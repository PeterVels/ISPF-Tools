/*  Rexx to create an org chart of the RACF group hierarchy
Need to sort these first!    sort 5 12 1 4
0102 DEVELOP  PETER    USE
0102 DEVELOP  DSBUTLE  USE
0102 DEVELOP  MPLUMME  USE
0102 DEVELOP  LDAWTRE  USE
0102 DEVELOP  SUBHOG   USE
0100 DEVELOP  SYS1     2021-03-25 ADCDMST
*/
address TSO
"execio * diskR topin  (finis"  /* FB 80     */
"execio * diskW topout (finis"  /* VB 1000   */
"execio * diskR botin  (finis"  /* FB 80     */
"execio * diskW botout (finis"  /* VB 1000   */
"execio * diskR infileG (stem in. finis"
group. = ''
counter = 0
do i = 1 to in.0
 parse var in.i type Xgroup Xname created owner .
 select
  when type = '0100' then do
    counter = counter + 1
    group.counter.name  = "{'v':'"Xgroup"', ",
                       ||  "'f':'"Xgroup,
                       || '<div style="color:green">',
                       || 'placeholder',
                       || "</div>'}"
    group.counter.creator = 'Owner:' owner
    group.counter.members = ''
    if Xgroup <> 'SYS1' then
     group.counter.super = "'"Xname"'"
    else
     group.counter.super = ''
  end
  when type = '0102' then
    group.counter.members = group.counter.members Xname
  otherwise
   nop
 end
end
do i = 1 to counter
 parse var group.i.name first_bit 'placeholder' second_bit
 field1 = first_bit || group.i.members || second_bit
 html.i = "["field1", "group.i.super" ,'"group.i.creator"'],"
end
"execio * diskW outfileG (stem html. finis)"
exit
